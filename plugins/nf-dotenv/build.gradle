/* Copyright 2021-2022, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'groovy'
    id 'idea'
}

group = 'io.nextflow'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

idea {
    module.inheritOutputDirs = true
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases' }
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/snapshots' }
}

configurations {
    // See https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = []
    test.resources.srcDirs = ['src/testResources']
}

dependencies {
    // This dependency is exported to consumers, that is to say, it is found on their compile classpath.
    compileOnly "io.nextflow:nextflow:$nextflowVersion"
    compileOnly 'org.slf4j:slf4j-api:1.7.10'
    compileOnly 'org.pf4j:pf4j:3.4.1'

    // Plugin dependencies.
    implementation 'io.github.cdimascio:dotenv-java:3.2.0'

    // Test configuration.
    testImplementation "io.nextflow:nextflow:$nextflowVersion"
    testImplementation "org.codehaus.groovy:groovy:$groovyVersion"
    testImplementation "org.codehaus.groovy:groovy-nio:$groovyVersion"
    testImplementation ("org.codehaus.groovy:groovy-test:$groovyVersion") { exclude group: 'org.codehaus.groovy' }
    testImplementation ("cglib:cglib-nodep:3.3.0")
    testImplementation ("org.objenesis:objenesis:3.1")
    testImplementation ("org.spockframework:spock-core:2.0-M3-groovy-3.0") { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
    testImplementation ('org.spockframework:spock-junit4:2.0-M3-groovy-3.0') { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }

    testImplementation(testFixtures("io.nextflow:nextflow:$nextflowVersion"))
    testImplementation(testFixtures("io.nextflow:nf-commons:$nextflowVersion"))

    testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // See https://docs.gradle.org/4.1/userguide/dependency_management.html#sec:module_replacement
    modules {
        module("commons-logging:commons-logging") { replacedBy("org.slf4j:jcl-over-slf4j") }
    }
}

// Use the JUnit 5 testing platform
test {
    useJUnitPlatform()
}
